buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/groups/public' }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.1.1'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'maven'

group = 'com.rackspace.prefs'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile('org.flywaydb:flyway-commandline:3.1') {
        transitive = false
    }
    compile 'org.flywaydb:flyway-core:3.1'
    compile 'com.h2database:h2:1.4.182'
}

task buildZip(type: Zip) {

    from sourceSets.main.output

    into('lib') {
        from configurations.runtime
    }
}

nexus {
    sign = false
    repositoryUrl = 'http://maven.research.rackspacecloud.com/content/repositories/releases/'
    snapshotRepositoryUrl = 'http://maven.research.rackspacecloud.com/content/repositories/snapshots/'
}

release {
    failOnPublishNeeded = false
    failOnCommitNeeded = false
    failOnUnversionedFiles = false
    tagPrefix = 'cloudfeeds-preferences-svc-db'
}

artifacts {
    archives(buildZip) {
        name 'cloudfeeds-preferences-svc-db'
        type 'zip'
        builtBy buildZip
    }
}

// 'gradle release' will also upload release to Nexus
uploadArchives.dependsOn   buildZip
createReleaseTag.dependsOn uploadArchives